
fn main {

    $products = getAllFromCart()

    $page = Page.create({
        title: 'Database Example',
        css: url'/vendor/basic.css',
        main: pageHtml($products),
    })

    return $page
}


// Modes (via 'mode' param in links)
// See:  https://tht.dev/reference/page-modes
//----------------------------------------------------------

fn addApplesMode {

    addToCart('apples', Math.random(1, 6))

    return true   // refresh page
}

fn addEggsMode {

    addToCart('eggs', 12)

    return true
}

fn deleteAllApplesMode {

    deleteFromCart('apples')

    return true
}

fn deleteAllEggsMode {

    deleteFromCart('eggs')

    return true
}


// Database Access
//----------------------------------------------------------

// Database: data/db/app.db (sqlite)
// Table: cart
// Columns:
//    product: varchar
//    quantity: int

fn getAllFromCart {

    return Db.selectRows(sql'select * from cart')
}

fn addToCart($product, $num) {

    Db.insertRow('cart', { product: $product, quantity: $num })
}

fn deleteFromCart($product) {

    Db.deleteRows('cart', { product: $product })
}


// Templates
//----------------------------------------------------------

tm pageHtml($products) {

    <h1> Database Example

    <table>
        <tr>
            <th> Product
            <th> Quantity
        </>
        --- foreach $products as $p {
        <tr>
            <td> {{ $p.product }}
            <td> {{ $p.quantity }}
        </>
        --- }
    </>

    --- if !$products {
        <p> <i> No items in cart.
    --- }


    {{ linksHtml() }}
}

tm linksHtml {

    <style>
        .links { margin-top: 6rem; width: 42rem; }
        .links button { margin: 1rem 0; width: 20rem; }
    </>

    <.links>
        {{ button('addApples') }}
        {{ button('deleteAllApples') }}
        {{ button('addEggs') }}
        {{ button('deleteAllEggs') }}
    </>
}

// Creates a button that submits data via a hidden form,
// wthout the need for AJAX.
fn button($mode) {

    $data = { mode: $mode }
    $label = $mode.humanize().titleCase()

    return Web.postLink($label, url'this', $data)
}

