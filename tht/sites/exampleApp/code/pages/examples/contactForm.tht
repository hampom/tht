

@@.form = Form.create('contactForm', {

    name: {
        tag: 'text',
        rule: 'name',
    },
    email: {
        tag: 'email',
    },
    message: {
        tag: 'textarea',
        rule: 'comment|min:10|max:100',
    },
    colors: {
        tag: 'radio',
        options: q[ red green blue ],
    },
    priority: {
        tag: 'select',
        rule: 'i',
        options: q[ 1 2 3 4 5 ],
    },
    toppings: {
        tag: 'checkbox',
        options: q[ pepperoni sausage onions mushrooms anchovies ],
    },
    accept: {
        tag: 'checkbox',
        rule: 'accepted',
        label: 'I accept this form.',
    },
    resume: {
        tag: 'file',
        rule: 'file|ext:doc,pdf,txt|dir:resumes|sizeKb:400',
    },
    profileImage: {
        tag: 'file',
        rule: 'image|dim:100,100|dir:profiles',
    },
})


// Runs when the page is first loaded.
fn main {

    $page = Page.create({
        title: 'Contact Form',
        main: formHtml(),
        css: [url'/vendor/basic.css'],
        js: [url'/vendor/form.js'],
    })

    $page.send()
}

// Runs when the form is submitted (i.e. request method is POST)
fn mainPost {

    // Validate input, then run the inner function
    @@.form.process(fn ($data) {

        // Example of custom validation
        if $data.name == 'troll' {
            return ['name', 'Please go somewhere else.']
        }

        // Save message to `data/logs/contact.log`
        File.log($data, 'contact.log')

        // Replace form with HTML fragment
        return thanksHtml($data.name)
    })
}

tm formHtml {

    <h1> Contact Us

    {{ @@.form.render(Web.icon('check') ~ html' Contact Us') }}
}

tm thanksHtml($name) {

    <p> Thanks <b>{{ $name }}</>, we will contact you soon!

    <p> <a href="/examples/contact-form"> Back to Form
}
