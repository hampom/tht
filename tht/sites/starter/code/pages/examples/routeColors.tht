fn main {

    // Get {color} route param, default to 'red'
    $color = Input.route('color', 'id', 'red')

    $page = Page.create({
        title: 'Colors',
        css: [url'vendor/basic.css'],
    })

    $html = pageHtml($color)
    $page.setMain($html)

    $page.send()
}

tm pageHtml($color) {

    <div style="border-top: solid 2rem {{ $color }}; padding-top: 1rem">

        <h1> {{ $color.upperCaseFirst() }}

        {{ linksHtml() }}

    </>
}

tm linksHtml {

    --- $colors = ['red', 'aqua', 'orange']

    --- foreach $colors as $color {

        <a.button href="/examples/colors-route/{{ $color }}">
            {{ $color.upperCaseFirst() }}
        </>

    --- }

    <.panel style="margin-top: 8rem">
        <p> This page uses a route defined in <code>config/app.jcon</code> as:
        <pre> /examples/colors-route/{color}: examples/colorsRoute.tht
    </>
}
