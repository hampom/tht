
@@.form = Form.create('contactForm', {

    name: {
        tag: 'text',
        rule: 'name',
    },
    email: {
        tag: 'email',
        rule: 'email|optional',
        help: 'We won`t send you junk mail.',
    },
    message: {
        tag: 'textarea',
        rule: 'comment|min:10|max:100',
    },
    priority: {
        tag: 'select',
        rule: 'id',
        options: {
            bug: 'Bug Report',
            suggestion: 'Suggestion',
            general: 'General Feedback',
        },
    },
    accept: {
        tag: 'checkbox',
        rule: 'accepted',
        label: 'I accept this form.',
    },
})


// Runs when the page is first loaded.
fn main {

    $page = Page.create({
        title: 'Contact Form',
        main: formHtml(),
        css: [url'/vendor/basic.css'],
        js: [url'/vendor/form.js'],
    })

    $page.send()
}

// Runs when the form is submitted (i.e. request method is POST)
fn postMode {

    // Validate input, then run the inner function
    @@.form.process(fn ($data) {

        // Example of custom validation
        if $data.name == 'troll' {
            return ['name', 'Please go somewhere else.']
        }

        File.log($data, 'contact.log')

        // Replace form with HTML fragment
        return thanksHtml($data.name)
    })
}

tm formHtml {

    <h1> Contact Form

    <.panel>
        --- $buttonLabel = Web.icon('check') ~ html' Contact Us'
        {{ @@.form.render($buttonLabel) }}
    </>

}

tm thanksHtml($name) {

    <p> Thanks <b>{{ $name }}</>, we will contact you soon!

    <p> The form data was written to <code>data/logs/contact.log</>

    <p> <a href="/examples/contact-form"> Back to Form
}
