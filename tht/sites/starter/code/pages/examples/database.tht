
fn main {

    $products = getAllFromCart()

    $page = Page.create({
        title: 'Database Example',
        css: url'/vendor/basic.css',
        main: pageHtml($products),
    })

    return $page
}


// Modes (via 'mode' param in links)
// See:  https://tht.dev/reference/page-modes
//----------------------------------------------------------

fn addTacosMode {

    addToCart('tacos', Math.random(2, 4))

    return true   // refresh page
}

fn addTatersMode {

    addToCart('taters', 6)

    return true
}

fn deleteAllTacosMode {

    deleteFromCart('tacos')

    return true
}

fn deleteAllTatersMode {

    deleteFromCart('taters')

    return true
}


// Database Access
//----------------------------------------------------------

// Database: data/db/app.db (sqlite)
// Table: cart
// Columns:
//    product: varchar
//    quantity: int

fn getAllFromCart {

    return Db.selectRows(sql'select * from cart')
}

fn addToCart($product, $num) {

    Db.insertRow('cart', { product: $product, quantity: $num })
}

fn deleteFromCart($product) {

    Db.deleteRows('cart', { product: $product })
}


// Templates
//----------------------------------------------------------

tm pageHtml($products) {

    <h1> Database Example

    <table>
        <tr>
            <th> Product
            <th> Quantity
        </>
        --- foreach $products as $p {
        <tr>
            <td> {{ $p.product }}
            <td> {{ $p.quantity }}
        </>
        --- }
    </>

    --- if !$products {
        <p> <i> No items in cart.
    --- }


    {{ linksHtml() }}
}

tm linksHtml {

    <style>
        .links { margin-top: 6rem; width: 42rem; }
        .links button { margin: 1.5rem 0; width: 20rem; }
    </>

    <.links>
        {{ button('addTacos', 'plus') }}
        {{ button('deleteAllTacos', 'cancel') }}
        {{ button('addTaters', 'plus') }}
        {{ button('deleteAllTaters', 'cancel') }}
    </>
}

// Creates a button that submits data via a hidden form,
// wthout the need for AJAX.
fn button($mode, $iconType) {

    $data = { mode: $mode }
    $label = $mode.humanize().toTitleCase()
    $iconLabel = iconLabelHtml($label, $iconType)

    return Web.postLink($iconLabel, url'this', $data)
}

tm iconLabelHtml($label, $iconType) {
    {{ Web.icon($iconType) }} {{ $label }}
}


