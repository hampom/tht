
@@.form = Form.create('uploadForm', {

    resume: {
        tag: 'file',
        rule: 'file|ext:doc,pdf,txt|dir:resumes|sizeKb:400',
        help: 'Allowed files: doc, pdf, txt -- Under 400 KB',
    },
    avatar: {
        tag: 'file',
        rule: 'image|dir:avatars|dim:100x100|exactSize',
        help: 'Allowed files: png, jpeg, gif',
    },
})


// Runs when the page is first loaded.
fn main {

    $page = Page.create({
        title: 'Upload Form',
        main: formHtml(),
        css: [url'/vendor/basic.css'],
        js: [url'/vendor/form.js'],
    })

    $page.send()
}

// Runs when the form is submitted (i.e. request method is POST)
fn postMode {

    // Validate input, then run the inner function
    @@.form.process(fn ($data) {

        $resumePath = $data.resume
        $avatarPath = $data.avatar

        // Replace form with HTML fragment
        return thanksHtml()
    })
}

tm formHtml {

    <h1> Upload Form

    <.panel>

        --- $buttonLabel = Web.icon('upload') ~ html' Upload Files'
        {{ @@.form.render($buttonLabel) }}

    </>

}

tm thanksHtml {

    <p> The files were validated and uploaded to <code>data/files</>.

    <p> <a href="/examples/upload-form"> Back to Form
}
