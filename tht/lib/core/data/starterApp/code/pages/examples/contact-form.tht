
@@.form = Form.create('contactForm', {

    name: {
        tag: 'text'
        rule: 'name'
    }
    email: {
        tag: 'email'
        rule: 'email|optional'
        help: 'We won`t send you junk mail.'
    }
    message: {
        tag: 'textarea'
        rule: 'comment|min:10|max:100'
    }
    priority: {
        tag: 'select'
        rule: 'id'
        options: {
            bug: 'Bug Report'
            suggestion: 'Suggestion'
            general: 'General Feedback'
        }
    }
    accept: {
        tag: 'checkbox'
        rule: 'accepted'
        label: 'I accept this form.'
    }
})


// Runs when the page is first loaded.
fun main {

    $page = Page.create({
        title: 'Contact Form'
        main: formHtml()
        css: [url'/vendor/basic.css']
        js: [url'/vendor/form.js']
    })

    Output.sendPage($page)
}

// Runs when the form is submitted (i.e. request method is POST)
fun postMode {

    // Validate input, then run the inner function
    @@.form.process(fun ($data) {

        // Example of custom validation
        if $data.name == 'troll' {
            return ['name', 'Please go somewhere else.']
        }

        Log.info($data)

        // Replace form with HTML fragment
        return thanksHtml($data.name)
    })
}

tem formHtml {

    <h1> Contact Form

    <.panel>
        --- $buttonLabel = Web.icon('check').append(html' Contact Us')
        {{ @@.form.toHtml($buttonLabel) }}
    </>
}

tem thanksHtml($name) {

    <p> Thanks <b>{{ $name }}</>, we will contact you soon!

    <p> The form data was written to <code>data/logs/app.log</>

    <p> <a href="/examples/contact-form"> Back to Form
}
